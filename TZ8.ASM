;******************************************************
;
;THIS IS AN EXAMPLE PROGRAM FOR THE Z8 MICROPROCESSOR.
;IT IS TAKEN FROM AN ARTICLE BY STEVE CIARCIA ON
;PAGE 120 & 122 OF THE OCTOBER 1984 ISSUE OF BYTE,
;AND AFTER ONLY FORMAT MODIFICATIONS FOR CROSS-16, IT
;IS PASSED ON AS AN EXAMPLE, FREE OF CHARGE, BY
;UNIVERSAL CROSS-ASSEMBLERS.

        HOF     "INT8"       ;INTEL HEX OUTPUT FORMAT
        CPU     "Z8.TBL"     ;CPU TABLE

;ALSO INCLUDED, IS A LIST OF THE REGISTER NAMES
;WITH THEIR CORRESPONDING ADDRESSES.  THESE MAY
;BE SPECIFIED AS PART OF THE ASSEMBLER CODE, OR
;MOVED TO THE END OF THE Z8 TABLE FOR GREATER
;TRANSPARENCY.

;CROSS-16 DOES NOT CHECK IF ADDRESSES FOR INSTRUCTIONS
;REQUIRING DOUBLE REGISTERS ARE EVEN.
;THE LOAD CONSTANT AUTOINCREMENT AND LOAD EXTERNAL DATA
;AUTOINCREMENT INSTRUCTIONS USE AN EXTRA 'R' TO SPECIFY
;THE DOUBLE REGISTER.  VALID EXAMPLES ARE:
;
        LDCI    @R5,@R R0
        LDCI    @R R0,@R5
;
        LDEI    @R8,@RR4
        LDEI    @RR4,@R8
;
SPL:    EQU     255          ;STACK POINTER LOW
SPH:    EQU     254          ;STACK POINTER HIGH
RP:     EQU     253          ;REGISTER POINTER
FLAGS:  EQU     252          ;PROGRAM CONTROL FLAGS
IMR:    EQU     251          ;INTERRUPT MASK REGISTER
IRQ:    EQU     250          ;INTERRUPT REQUEST REG.
IPR:    EQU     249          ;INTERRUPT PRIORITY REG.
P01M:   EQU     248          ;PORTS 0-1 MODE
P3M:    EQU     247          ;PORT 3 MODE
P2M:    EQU     246          ;PORT 2 MODE
PRE0:   EQU     245          ;T0 PRESCALER LOAD
T0:     EQU     244          ;TIMER/COUNTER 0 LOAD
PRE1:   EQU     243          ;T1 PRESCALER LOAD
T1:     EQU     242          ;TIMER/COUNTER 1 LOAD
TMR:    EQU     241          ;TIMER MODE REGISTER
SIO:    EQU     240          ;SERIAL I/O
P3:     EQU     3            ;PORT 3
P2:     EQU     2            ;PORT 2
P1:     EQU     1            ;PORT 1
P0:     EQU     0            ;PORT 0

;******************************************************
;
; Z8 VERSION RANGE-FINDER ROUTINES
;
; DRIVE INIT LINE AND SET UP CLOCKS
;
        ORG     1500H
INIT:   EQU     $
        DI                   ;SHUT DOWN INTERRUPTS
        OR      IMR,#20H     ;ENABLE IRQ 5 (TIMER 1)
        LD      02H,#02      ;TURN ON INIT LINE
        OR      TMR,#0CH     ;LOAD AND START TIMER 1
        EI                   ;ENABLE INTERRUPTS
        RET                  ;RETURN TO BASIC
;
;
; THIS IS THE INTERRUPT-DRIVEN RANGING ROUTINE
;
;
RANGE:  EQU     $
        INC     30H          ;ADD 1 TO PERIOD COUNTER
        JR      NZ,ECHO      ;IF NO WRAP, CHECK ECHO
        LD      30H,#0FFH    ;SET COUNT TO %FF AND
        JR      DONE         ;SHUT DOWN RANGE ROUTINE
ECHO:   EQU     $
        TM      02H,#01      ;IS ECHO BIT ON?
        JR      Z,EXIT       ;NO, MAKE ANOTHER PASS
DONE:   EQU     $
        LD      31H,#0FFH    ;TURN ON DONE FLAG
        AND     TMR,#0F3H    ;TURN OFF T1 TIMER
        AND     IMR,#1FH     ;DISABLE IRQ 5 INTERRUPT
        LD      02H,#0       ;SHUT OFF INIT LINE
EXIT:   EQU     $
        IRET                 ;RETURN FROM INTERRUPT
;
;END OF MACHINE-LANGUAGE ROUTINES
;
ZZZZ:   EQU     $

        END
